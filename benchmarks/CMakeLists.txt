#
# FFT Benchmarks
#

macro(fiber_add_benchmark fiber_benchmark)

    add_executable(${fiber_benchmark} "${fiber_benchmark}.c")
    target_link_libraries(${fiber_benchmark} ${MPI_C_LIBRARIES})
    if (FIBER_ENABLE_CUDA)
        target_link_libraries(${fiber_benchmark} ${CUDA_LIBRARIES})
    endif()
    target_include_directories(${fiber_benchmark} PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/../include/>)

    if (FIBER_ENABLE_HEFFTE)
        target_link_libraries(${fiber_benchmark} heffte)
    endif()
    if (FIBER_ENABLE_FFTMPI)
        target_link_libraries(${fiber_benchmark} fft3dmpi)
    endif()
    if (FIBER_ENABLE_ACCFFT)
        # target_link_libraries(${fiber_benchmark} accfft accfft_gpu  accfft_utils accfft_gpu_utils stdc++ fftw3_mpi fftw3_threads)
        target_link_libraries(${fiber_benchmark} accfft fftw3_threads)
    endif()
    if (FIBER_ENABLE_P3DFFT)
        target_link_libraries(${fiber_benchmark} p3dfft.3 stdc++ fftw3 fftw3f)
    endif()
    if (FIBER_ENABLE_FFTE)
        target_link_libraries(${fiber_benchmark} fftempi ffte ${MPI_Fortran_LIBRARIES} ${CMAKE_Fortran_IMPLICIT_LINK_LIBRARIES})
    endif()
    if (FIBER_ENABLE_SWFFT)
        target_link_libraries(${fiber_benchmark} fiber_swfft)
    endif()
    if (FIBER_ENABLE_2DECOMP)
        target_link_libraries(${fiber_benchmark} mpi_usempif08 mpi_usempi_ignore_tkr mpi_mpifh 2decomp_fft_iface 2decomp_fft fftw3f fftw3 gfortran)
    endif()
    if (FIBER_ENABLE_NB3DFFT)
        target_link_libraries(${fiber_benchmark} nb3dfft)
    endif()
    if (FIBER_ENABLE_FFTW)
        target_link_libraries(${fiber_benchmark} fftw3_mpi fftw3)
    endif()

endmacro()

macro(fiber_add_mpi_test)
    cmake_parse_arguments(_fiber "" "NAME;COMMAND;RANKS" "" ${ARGN} )
    add_test(${_fiber_NAME} ${MPIEXEC_EXECUTABLE} ${MPIEXEC_NUMPROC_FLAG} ${_fiber_RANKS} ${MPIEXEC_PREFLAGS} ${CMAKE_CURRENT_BINARY_DIR}/${_fiber_COMMAND} ${MPIEXEC_POSTFLAGS})
    unset(_fiber_NAME)
    unset(_fiber_RANKS)
    unset(_fiber_COMMAND)
endmacro()

if (FIBER_ENABLE_FFTE)
    link_directories(${CMAKE_Fortran_IMPLICIT_LINK_DIRECTORIES})
endif() 

fiber_add_benchmark(test3D_CPU_R2C)
fiber_add_benchmark(test3D_CPU_C2C)
fiber_add_benchmark(Bench_3D_C2C_CPU)

fiber_add_benchmark(test3D_C2C)

if (FIBER_ENABLE_CUDA)
    fiber_add_benchmark(test3D_GPU_R2C)
    fiber_add_benchmark(test3D_GPU_C2C)
endif() 
